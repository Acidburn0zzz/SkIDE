import java.nio.file.Paths

buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.3.2'
        classpath 'net.sf.proguard:proguard-gradle:5.3.3'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}
apply plugin: 'kotlin'
apply plugin: 'application'

group = 'com.skide-ultimate'
version = "2019.2"
mainClassName = 'com.skide.MainKt'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    flatDir {
        dirs 'static-libs'
    }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.controlsfx:controlsfx:8.40.14'
    implementation "org.json:json:20180813"
    implementation name: 'terminalfx-1.3'
    implementation 'com.jcraft:jsch:0.1.54'
    implementation 'org.apache.httpcomponents:httpclient:4.5.3'
    implementation 'commons-net:commons-net:3.6'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"


}
compileKotlin {
    kotlinOptions.jvmTarget = project.targetCompatibility
}
compileTestKotlin {
    kotlinOptions.jvmTarget = project.targetCompatibility
}
shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    archiveName = "ide.jar"
}
task proguard(type: proguard.gradle.ProGuardTask) {

    configuration 'proguard.txt'
    injars 'build/libs/ide.jar'
    outjars 'build/libs/pro.jar'

    keep 'public class com.skide.MainKt { \
        public static void main(java.lang.String[]); \
    }'
}
jar {
    archiveName = "ide-core.jar"
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Name': name
        attributes 'Specification-Title': name
        attributes 'Specification-Version': version
        attributes 'Specification-Vendor': name
        attributes 'Implementation-Title': name
        attributes 'Implementation-Version': version
        attributes 'Implementation-Vendor': name
    }
}
